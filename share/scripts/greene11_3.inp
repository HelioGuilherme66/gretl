(* script to replicate Example 11.3 in Greene *)
open greene11_3.gdt
(* run initial OLS *)
ols C 0 Y
genr essbak = $ess
genr essdiff = 1
genr b0 = coeff(Y)
genr gamma0 = 1
(* form the linearized variables *)
genr C0 = C + gamma0 * b0 * Y^gamma0 * log(Y)
genr x1 = Y^gamma0
genr x2 = b0 * Y^gamma0 * log(Y)
(* iterate OLS till the error sum of squares converges *)
loop while essdiff > .00001
   ols C0 0 x1 x2 -o
   genr b0 = coeff(x1)
   genr gamma0 = coeff(x2)
   genr C0 = C + gamma0 * b0 * Y^gamma0 * log(Y)
   genr x1 = Y^gamma0
   genr x2 = b0 * Y^gamma0 * log(Y)
   genr ess = $ess
   genr essdiff = abs(ess - essbak)/essbak
   genr essbak = ess
endloop 
(* print parameter estimates using their "proper names" *)
genr alpha = coeff(0)
genr beta = coeff(x1)
genr gamma = coeff(x2) 
print alpha beta gamma 
