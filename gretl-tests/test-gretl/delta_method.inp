function matrix MPC(matrix *param, matrix *Y)
  beta = param[2]
  gamma = param[3]
  y = Y[1]

  silly = 0

  if silly=1
    matrix ret = zeros(2,1)
    ret[1] = beta*gamma*y^(gamma-1)
  else
    matrix ret = beta*gamma*y^(gamma-1)
  endif 

  return ret
end function

# William Greene, Econometric Analysis, 5e, Chapter 9
set echo off
set messages off
open greene5_1.gdt

# Use OLS to initialize the parameters
ols realcons 0 realdpi --quiet
genr a = $coeff(0)
genr b = $coeff(realdpi)
genr g = 1.0

# Run NLS with analytical derivatives
nls realcons = a + b * (realdpi^g)
 deriv a = 1
 deriv b = realdpi^g
 deriv g = b * realdpi^g * log(realdpi)
end nls

matrix Y = realdpi[2000:4]
matrix theta = $coeff
matrix V = $vcv

mpc = MPC(&theta, &Y)
matrix Jac = fdjac(theta, MPC(&theta, &Y))
Sigma = qform(Jac, V)
scalar se = sqrt(Sigma)

printf "\nmpc = %g, std.err = %g\n", mpc, se
scalar teststat = (mpc-1)/se
printf "\nTest for MPC = 1: %g (p-value = %g)\n", \
	teststat, pvalue(n,abs(teststat))

