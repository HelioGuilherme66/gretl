open data4-1 -q

# estimate equation by equation, and restrict

ols 1 0 2 -q
restrict --full
 b1 = 50
end restrict
matrix B1 = $coeff
matrix SE1 = $stderr

ols 3 0 2 -q
restrict --full
 b1 = 3
end restrict
B1 ~= $coeff
SE1 ~= $stderr

# estimate as system, and restrict

s0 <- system
  equation 1 0 2
  equation 3 0 2
end system

restrict s0
  b[1,1] = 50
  b[2,1] = 3
end restrict

estimate s0 method=ols
matrix B2 = mshape($coeff, 2, 2)
matrix SE2 = mshape($stderr, 2, 2)

# restricted estimates via matrix method (mrls)

matrix Y = {price, bedrms}
matrix X = {const, sqft}
matrix R = {1, 0, 0, 0 ; 0, 0, 1, 0}
matrix Q = {50 ; 3}
matrix U
matrix V

matrix B3 = mrls(Y, X, R, Q, &U, &V)
SSR = sumc(U .* U)
printf "SSR = \n%#14.7g\n", SSR
sigma = sdc(U)
sigma *= sqrt(14/13)
printf "sigma = \n%#12.6g\n", sigma
V1 = sigma[1]^2 * V[1:2,1:2]
V2 = sigma[2]^2 * V[3:4,3:4]
matrix SE3 = sqrt(diag(V1)) ~ sqrt(diag(V2))

# compare
print B1 B2 B3
print SE1 SE2 SE3






