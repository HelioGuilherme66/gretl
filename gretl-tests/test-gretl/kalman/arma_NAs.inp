function void arma11_via_kalman(series y)
    /* parameter initalization */
    phi = 0
    theta = 0
    sigma = 1
    /* Kalman filter setup */
    matrix H = {1; theta}
    matrix F = {phi, 0; 1, 0}
    matrix Q = {sigma^2, 0; 0, 0}
    kalman
        obsy y
        obsymat H
        statemat F
        statevar Q
    end kalman

    matrix E
    matrix V

    /* maximum likelihood estimation */
    mle logl = ERR ? NA : misszero(llt)
        H[2] = theta
        F[1,1] = phi
        Q[1,1] = sigma^2
        ERR = kfilter()
	series llt = $kalman_llt
	
        params phi theta sigma
    end mle -h
end function
# ------------------------ main ---------------------------
nulldata 40
setobs 1 1 --special
set seed 6754831
u = normal()
series y = 0
y = 0.8*y(-1) + u + 0.2*u(-1)
y[30] = NA

arma11_via_kalman(y) # estimate an arma(1,1) model

foreign language=R --send-data
    y <- gretldata[,"y"]
    arima(y, c(1,0,1), include.mean=F)
end foreign
