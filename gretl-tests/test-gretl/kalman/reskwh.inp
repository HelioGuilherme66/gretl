function matrix bsm (series y)
    smpl ok(y) --restrict

    scalar seps = 10
    scalar seta = 10
    scalar szeta = 10
    
    matrix F = {1,1;0,1} 
    matrix Sigma1 = { 10 }
    matrix A = { 1 ; 0 }    

    p = $pd   
    if p > 1
        scalar somega = 10
        matrix F2 = -ones(1,p-1) | (I(p-2) ~ zeros(p-2,1))
        F = ( F ~ zeros(2,p-1) ) | (zeros(p-1,2) ~ F2)
        matrix Sigma2 = zeros(p+1,p+1)
        Sigma2[1:3,1:3] = 10*I(3)
        A = A | {1} | zeros(p-2,1)
    else
        matrix Sigma2 = I(2)
    endif
    
    /* Kalman filter set-up */
    kalman
       obsy y
       obsymat A
       obsvar Sigma1
       statemat F
       statevar Sigma2
   end kalman --diffuse
   
    /* ML estimation */
    mle ll = ERR ? NA : $kalman_llt
        Sigma1[1] = seps^2
        Sigma2[1,1] = seta^2
        Sigma2[2,2] = szeta^2
        Sigma2[3,3] = somega^2
        ERR = kfilter()
        params seps seta szeta somega
    end mle
    matrix ret = abs({seps, seta, szeta, somega})
    return ret
end function

function list bsm_smooth (series y, matrix vars)
    /* return the smoothed estimate of \mu_t */
    smpl ok(y) --restrict

    scalar seps = vars[1]
    scalar seta = vars[2]
    scalar szeta = vars[3]

    matrix F = {1,1;0,1} 
    matrix Sigma1 = { seps^2 }
    matrix A = { 1 ; 0 }    

    p = $pd   
    if p > 1
        scalar somega = vars[4]
        matrix F2 = -ones(1,p-1) | (I(p-2) ~ zeros(p-2,1))
        F = ( F ~ zeros(2,p-1) ) | (zeros(p-1,2) ~ F2)
        matrix Sigma2 = zeros(p+1,p+1)
        Sigma2[1,1] = seta^2
        Sigma2[2,2] = szeta^2
        Sigma2[3,3] = somega^2
        A = A | {1} | zeros(p-2,1)
    else
        matrix Sigma2 = { seta, 0 ; 0, szeta }
    endif
      
    /* Kalman filter set-up */
    kalman
       obsy y
       obsymat A
       obsvar Sigma1
       statemat F
       statevar Sigma2
   end kalman --diffuse

   matrix smst = ksmooth()
   series level = smst[,1]
   setinfo level -d "level component"
   series slope = smst[,2]
   setinfo slope -d "slope"
   series seasonal = smst[,3]
   setinfo seasonal -d "seasonal component"
   
   list ret = level slope seasonal
   return ret
end function

open data9-3
y = reskwh
# estimate the basic structural model
vars = bsm(y)
matrix V = (vars.^2)'
V
# compute the smoothed states
# list X = bsm_smooth(y, vars)
# print X -o
