set verbose off
include SVAR.gfn
open sw_ch14.gdt

series infl = 400*ldiff(PUNEW)
rename LHUR unemp

list X = unemp infl
list Z = const

Mod = SVAR_setup("C", X, Z, 3)
Mod.horizon = 36
SVAR_restrict(&Mod, "C", 1, 2)

set stopwatch
SVAR_estimate(&Mod)
printf "Time (Cmodel) = %g\n", $stopwatch

fevdmat = FEVD(&Mod)
print fevdmat
IRFplot(&Mod, 1, 1)
IRFsave("simple_C_11_noboot.pdf", &Mod, 1, 1)

set stopwatch
bfail = SVAR_boot(&Mod, 1024, 0.90)
printf "Number of failed bootstrap iterations = %d, Time (bootstrap) = %g\n", \
  bfail, $stopwatch

gpbuild Plots
    IRFplot(&Mod, 0, 0)
end gpbuild

gridplot Plots --output=display --width=1200
IRFsave("simpleC.pdf", &Mod, 0, 0)	# 0 means "all"
