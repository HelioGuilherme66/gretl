# Example script for sign restrictions in gretl's SVAR addon

set verbose off
set seed 909090

include SVAR.gfn

###
### --- Uhlig (2005) example
###

# data handling

open UhligData.gdt --quiet --frompkg=SVAR
series y = 100 * log(RealGDP)
series p = 100 * log(CPI)
series cp = 100 * log(CommodityPrice)
series r = FedFund
series nbr = 100 * log(NonBorrReserves)
series res = 100 * log(TotalReserves)

# set up the model

list X = y p cp r nbr res
list Z = const
scalar order = 12

bundle mod = SVAR_setup("plain", X, Z, order, 0)
mod.horizon = 60
mod.snames[4] = "Monetary"
err = SVAR_estimate(&mod,0)

# replicate page 395 (sort of)
if 0
    bfail = SVAR_boot(&mod, 1024, 0.63)
    loop i=1..6 --quiet
        IRFplot(&mod, 4, i)
    endloop
endif

# Switch to a sign restriction model, but re-using the 
# reduced-form results from above

mod.type = 10 # (10 is internal code for SR)

# set up the sign restictions

SVAR_SRplain(&mod, "p", "Monetary", "-", 5)    # negative from 0 to 5
SVAR_SRplain(&mod, "nbr", "Monetary", "-", 5)  # ditto
SVAR_SRplain(&mod, "cp", "Monetary", "-", 5)   # ditto
SVAR_SRplain(&mod, "r", "Monetary", "+", 5)    # positive from 0 to 5

# do the rotations (this is the computationally intensive part)
bundles moo = SVAR_setidIRF(mod, 200, 1)

# draw the irfs (page 397)
bundle IrfData = IRF_plotdata(moo, 0.75)

k = 1
loop foreach i X --quiet
    vname = "$i"
    matrix grph = IrfData.irfSRmeans[,k] ~ seq(0, mod.horizon)'
    matrix band = IrfData.lo_cb[,k] ~ IrfData.hi_cb[,k]
    band = 0.5 * band * {1, 1; 1, -1}

    plot grph
        options with-lines fit=none
        options band=band,1 band-style=fill
        printf "set title '%s shock -> %s'", mod.snames[1], vname
    end plot --output=display
    k++
endloop
