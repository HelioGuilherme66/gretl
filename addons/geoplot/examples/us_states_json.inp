include geoplot.gfn

string JSON = "us-states.geojson"

open @JSON -q -p

# construct a suitable key series
strings S1 = strvals(iso_3166_2)
strings S2 = array($nobs)
loop i=1..$nobs -q
  S2[i] = S1[i] + 3
endloop
series Code = iso_3166_2
stringify(Code, S2)
# print iso_3166_2 Code -o

join statepop.gdt population --ikey=Code
print -o name Code population

# cut out Hawaii and Alaska
series mask = 0
smpl Code=="HI" || Code=="AK" --restrict
population = NA
mask = NA
smpl full

bundle options = null
options.plotfile = "us_states_json.plt"
options.height = 500

outlines_only = 1

if outlines_only
   # cut out Hawaii and Alaska via mask
   set skip_missing off
   options.mask = {mask}
   geoplot(JSON, null, options)
else
   # show population levels in shades of blue
   options.setpal = "set palette defined (0 '#D4E4F2', 1 'steelblue')"
   options.title = "US states, population (lower 48)"
   geoplot(JSON, population, options)
endif

